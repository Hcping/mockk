buildscript {
    ext.kotlin_version = '1.2.21'
    repositories {
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

if (project.hasProperty('JDK6_HOME')) {
    tasks.withType(JavaCompile) {
        options.bootClasspath = "$JDK6_HOME/jre/lib/rt.jar"
    }
}

subprojects {
    group = 'io.mockk'

    apply plugin: 'maven'
    apply plugin: 'signing'


    repositories {
        mavenCentral()
    }

    signing {
        sign configurations.archives
    }

    signArchives.onlyIf { project.hasProperty('signing.keyId') }

    if (!it.name.equals('mockk-android-agent')) {
        apply plugin: 'java'

        sourceCompatibility = 1.6
        targetCompatibility = 1.6

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        artifacts {
            archives javadocJar
        }

        if (project.hasProperty('JDK6_HOME')) {
            tasks.withType(JavaCompile) {
                options.bootClasspath = "$JDK6_HOME/jre/lib/rt.jar"
            }
        }
    }

}