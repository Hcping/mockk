buildscript {
    ext.kotlin_version = '1.2.41'
    ext.kotlin_gradle_version = '1.2.41'
    ext.android_gradle_version = '3.1.2'
    ext.byte_buddy_version = '1.8.8'
    ext.coroutines_version = '0.22'
    ext.dexmaker_version = '2.12.1'
    ext.objenesis_version = '2.6'
    ext.junit_version = '4.12'
    ext.junit_jupiter_version = '5.0.3'
    ext.junit_vintage_version = '4.12.0'

    repositories {
        mavenCentral()
        google()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_gradle_version"
        classpath "com.android.tools.build:gradle:$android_gradle_version"
    }
}

if (project.hasProperty('JDK6_HOME')) {
    tasks.withType(JavaCompile) {
        options.bootClasspath = "$JDK6_HOME/jre/lib/rt.jar"
    }
}

subprojects {
    group = 'io.mockk'

    apply plugin: 'maven'
    apply plugin: 'signing'


    repositories {
        mavenCentral()
        maven {
            url = "https://dl.bintray.com/linkedin/maven/"
        }
        google()
        jcenter()
    }

    signing {
        sign configurations.archives
    }

    signArchives.onlyIf { project.hasProperty('signing.keyId') }

    if (!it.name.contains('android')) {
        apply plugin: 'java'

        sourceCompatibility = 1.6
        targetCompatibility = 1.6

        task javadocJar(type: Jar) {
            classifier = 'javadoc'
            from javadoc
        }

        artifacts {
            archives javadocJar
        }

        if (project.hasProperty('JDK6_HOME')) {
            tasks.withType(JavaCompile) {
                options.bootClasspath = "$JDK6_HOME/jre/lib/rt.jar"
            }
        }
    }

}